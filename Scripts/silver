USE DataWarehouse
GO

CREATE OR ALTER PROCEDURE silver.load_silver as 
BEGIN 
DECLARE @start_time DATETIME, @end_time DATETIME, @batch_start_time DATETIME, @batch_end_time DATETIME; 
BEGIN TRY 
		SET @batch_start_time = GETDATE();
		PRINT '===========================================';
		PRINT 'lOADING SILVER LAYER';
		PRINT '===========================================';

		PRINT '-------------------------------------------';
		PRINT 'LOADING CRM TABLE';
		PRINT '-------------------------------------------';

SET @start_time = GETDATE();
PRINT '>> TRUNCATING TABLE: silver.crm_cust_info';
TRUNCATE table silver.crm_cust_info
PRINT '>> INSERTING DATA INTO: silver.crm_cust_info';
INSERT INTO silver.crm_cust_info (
	cst_id,
	cst_key,
	cst_firstname,
	cst_lastname,
	cst_marital_status,
	cst_gndr,
	cst_create_date
	)

select 
cst_id,
cst_key,
TRIM(cst_firstname) AS cst_firstname,
TRIM(cst_lastname) AS cst_lastname,
CASE WHEN UPPER(TRIM(cst_marital_status)) = 'M' THEN 'Married'
	 WHEN UPPER(TRIM(cst_marital_status)) = 'S' THEN 'Single'
	 ELSE 'n/a'
END AS cst_marital_status,
CASE WHEN UPPER(TRIM(cst_gndr)) = 'F' THEN 'Female'
	 WHEN UPPER(TRIM(cst_gndr)) = 'M' THEN 'Male'
	 ELSE 'n/a'
END AS cst_gndr,
cst_create_date
from (
select *,
ROW_NUMBER() OVER (PARTITION BY cst_id order by cst_create_date desc)as flag_last
from bronze.crm_cust_info
)t where flag_last=1
		SET @end_time = GETDATE();
		PRINT '>> Load Duration: ' + CAST(DATEDIFF(second, @start_time, @end_time) AS NVARCHAR) + ' seconds';
		PRINT '>> ------------------------------------------';

--select distinct cst_marital_status from silver.crm_cust_info
SET @start_time = GETDATE();
PRINT '>> TRUNCATING TABLE: silver.crm_prd_info';
TRUNCATE TABLE silver.crm_prd_info
PRINT '>> INSERTING DATA INTO: silver.crm_prd_info';
INSERT INTO silver.crm_prd_info (
	prd_id,
	cat_id,
	prd_key,
	prd_nm,
	prd_cost,
	prd_line,
	prd_start_dt,
	prd_end_dt
	)
select 
	prd_id,
	REPLACE(SUBSTRING(prd_key, 1, 5), '-','_') AS cat_id,
	SUBSTRING(prd_key, 7, len(prd_key)) as prd_key,
	prd_nm,
	isnull(prd_cost, 0)as prd_cost,
	CASE UPPER(TRIM(prd_line))
		WHEN 'M' THEN 'Mountain'
		WHEN 'R' THEN 'Road'
		WHEN 'S' THEN 'Other Sales'
		WHEN 'T' THEN 'TOURING'
		ELSE 'n/a'
	END AS prd_line,
	cast(prd_start_dt as DATE)as prd_start_dt,
	cast(LEAD(prd_start_dt) OVER (PARTITION BY prd_key order by prd_start_dt)-1 as DATE) as prd_end_dt
from bronze.crm_prd_info
		SET @end_time = GETDATE();
		PRINT '>> Load Duration: ' + CAST(DATEDIFF(second, @start_time, @end_time) AS NVARCHAR) + ' seconds';
		PRINT '>> ------------------------------------------';

--SILVER CRM SALES TABLE 
SET @start_time = GETDATE();
PRINT '>> TRUNCATING TABLE: silver.crm_sales_details';
TRUNCATE TABLE silver.crm_sales_details
PRINT '>> INSERTING DATA INTO: silver.crm_sales_details';
INSERT INTO silver.crm_sales_details(
	sls_ord_num,
	sls_prd_key,
	sls_cust_id,
    sls_order_dt, 
    sls_ship_dt,  
    sls_due_dt,   
    sls_sales,    
    sls_quantity, 
    sls_price)

SELECT 
	sls_ord_num,
	sls_prd_key,
	sls_cust_id,
CASE WHEN sls_order_dt = 0 OR LEN(sls_order_dt)!=8 THEN NULL 
	ELSE CAST(CAST(sls_order_dt AS VARCHAR)AS DATE)
END AS sls_order_dt, 
CASE WHEN sls_ship_dt= 0 OR LEN(sls_ship_dt)!=8 THEN NULL 
	ELSE CAST(CAST(sls_ship_dt AS VARCHAR)AS DATE)
END AS sls_ship_dt, 
CASE WHEN sls_due_dt = 0 OR LEN(sls_due_dt)!=8 THEN NULL 
	ELSE CAST(CAST(sls_due_dt AS VARCHAR)AS DATE)
END AS sls_due_dt,   
CASE WHEN sls_sales!=sls_quantity*ABS(sls_price) OR sls_sales IS NULL OR sls_sales<=0 
	THEN sls_quantity * ABS(sls_price)ELSE sls_sales 
END AS sls_sales,
	sls_quantity,
CASE WHEN sls_price<=0 or sls_price is null THEN sls_sales/NULLIF(sls_quantity,0) 
    ELSE sls_price
END AS sls_price

FROM bronze.crm_sales_details
		SET @end_time = GETDATE();
		PRINT '>> Load Duration: ' + CAST(DATEDIFF(second, @start_time, @end_time) AS NVARCHAR) + ' seconds';
		PRINT '>> ------------------------------------------';

SET @start_time = GETDATE();
PRINT '>> TRUNCATING TABLE: silver.erp_cust_az12';
TRUNCATE TABLE silver.erp_cust_az12
PRINT '>> INSERTING DATA INTO: silver.erp_cust_az12';
INSERT INTO silver.erp_cust_az12(
	cid,
    bdate,
    gen
	)

SELECT 
CASE WHEN 
	cid LIKE '%NAS%' THEN SUBSTRING(cid,4,LEN(cid)) else cid
END AS cid,
CASE WHEN bdate>GETDATE()THEN NULL
	ELSE bdate
END AS bdate,
CASE 
	WHEN UPPER(TRIM(gen)) IN ('F', 'FEMALE') THEN 'Female'
	WHEN UPPER(TRIM(gen)) IN ('M', 'MALE') THEN 'Male'
	ELSE 'n/a'
END AS gen
FROM bronze.erp_cust_az12
		SET @end_time = GETDATE();
		PRINT '>> Load Duration: ' + CAST(DATEDIFF(second, @start_time, @end_time) AS NVARCHAR) + ' seconds';
		PRINT '>> ------------------------------------------';


SET @start_time = GETDATE();
PRINT '>> TRUNCATING TABLE: silver.erp_loc_a101';
TRUNCATE TABLE silver.erp_loc_a101
PRINT '>> INSERTING DATA INTO: silver.erp_loc_a101';
INSERT INTO silver.erp_loc_a101(
cid,
cntry
)

SELECT 
REPLACE(cid, '-', '')as cid, 
CASE WHEN TRIM(cntry) = 'DU' THEN 'Germany'
	 WHEN TRIM(cntry) IN ('US','USA') THEN 'United States'
	 WHEN TRIM(cntry) = '' or cntry is null THEN 'n/a'
	 ELSE TRIM(cntry)
end as cntry 
FROM bronze.erp_loc_a101
		SET @end_time = GETDATE();
		PRINT '>> Load Duration: ' + CAST(DATEDIFF(second, @start_time, @end_time) AS NVARCHAR) + ' seconds';
		PRINT '>> ------------------------------------------';

SET @start_time = GETDATE();
PRINT '>> TRUNCATING TABLE: silver.erp_px_cat_g1v2';
TRUNCATE TABLE silver.erp_px_cat_g1v2
PRINT '>> INSERTING DATA INTO: silver.erp_px_cat_g1v2';
INSERT INTO silver.erp_px_cat_g1v2(
    id,
    cat,
    subcat,
    maintenance
	)

SELECT 
id,
cat,
subcat,
maintenance
FROM bronze.erp_px_cat_g1v2
		SET @end_time = GETDATE();
		PRINT '>> Load Duration: ' + CAST(DATEDIFF(second, @start_time, @end_time) AS NVARCHAR) + ' seconds';
		PRINT '>> ------------------------------------------';


SET @batch_end_time = GETDATE()
PRINT '==========================================';
		PRINT 'Loading Silver Layer is Completed';
        PRINT ' -Total Load Duration: ' + CAST(DATEDIFF(SECOND, @batch_start_time, @batch_end_time) AS NVARCHAR) + ' seconds';
PRINT '==========================================';

END TRY
BEGIN CATCH 
	PRINT '============================================';
	PRINT 'ERROR MESSAGE'+ ERROR_MESSAGE();
	PRINT 'ERROR NUMBER'+ CAST(ERROR_NUMBER()AS NVARCHAR);
	PRINT 'ERROR STATE'+ CAST(ERROR_STATE()AS NVARCHAR);
	PRINT '============================================';
END CATCH 
END; 

GO 
EXEC silver.load_silver;

